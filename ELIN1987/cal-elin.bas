70 REM ***************************************
75 REM ***   BERAKNING AV ELEKTRISKA FALT   ***
77 REM ***        TVACYLINDER TYP           ***
78 REM ***           CA-E1-EF
80 REM ***************************************
100 REM DIMENSIONER
110 NX=30
120 NY=15
130 NX1=4
140 NX2=14
150 NX3=18
160 NX4=24
170 NY1=5
180 NY2=4
190 NY3=12
200 NY4=11
210 EPS=5
230 DIM V(NX,NY),U(NX,NY)
300 REM START OCH RANDVARDEN
310 VO=0
320 V1=-40
330 V2=40
340 FOR I=1 TO NX
350 FOR J=1 TO NY
360 V(I,J)=VO
370 NEXT J
380 NEXT I
400 FOR J=NY1 TO NY4 STEP NY4-NY1
410 FOR I=NX1 TO NX2
420 V(I,J)=V1
430 NEXT I
440 NEXT J
450 FOR J=NY2 TO NY3 STEP NY3-NY2
460 FOR I=NX3 TO NX4
470 V(I,J)=V2
480 NEXT I
490 NEXT J
590 ITNR=0
600 REM INTEGRATIONSLOOP
610 FEL=0
620 FOR I=2 TO NX-1
630 FOR J=2 TO NY-1
640 IF V(I,J)=V1 OR V(I,J)=V2 THEN 656
650 U(I,J)=(V(I,J-1)+V(I,J+1)+V(I+1,J)+V(I-1,J))/4
654 GOTO 660
656 U(I,J)=V(I,J)
660 NEXT J
670 NEXT I
680 FOR I=2 TO NX-1
690 FOR J=2 TO NY-1
700 FEL=FEL+ABS(V(I,J)-U(I,J))
710 V(I,J)=U(I,J)
720 NEXT J
730 NEXT I
740 ITNR=ITNR+1
750 PRINT "ITERATIONSNUMMER=";ITNR,"FEL=";FEL
760 IF FEL>EPS THEN 600
800 OPEN "SLASK.TXT" FOR OUTPUT AS #1
810 PRINT #1,NX,NY,NX1,NX2,NX3,NX4
820 PRINT #1,NY1,NY2,NY3,NY4
830 PRINT #1, VO,V1,V2
840 PRINT
850 PRINT "POTENTIALFŽLTET BLIR"
860 PRINT
870 PRINT "      ";
880 FOR J= 1 TO NY
890 IF J>9.5 THEN 910
900 PRINT " ";
910 PRINT J;" ";
920 NEXT J
930 PRINT
935 PRINT
940 FOR I=1 TO NX
950 IF I>9.5 THEN 970
960 PRINT " ";
970 PRINT I;"  ";
980 FOR J=1 TO NY
990 PRINT #1, V(I,J);
1000 PT=INT(V(I,J)+.5)
1010 IF ABS(PT)>9.5 THEN 1030
1020 PRINT " ";
1030 PRINT PT;" ";
1040 NEXT J
1050 PRINT
1060 NEXT I
1070 CLOSE
1080 END
